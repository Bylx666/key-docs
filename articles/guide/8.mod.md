# 模块化

模块使用`mod`关键字导入和导出. 

目前模块只允许导出函数和类.

导入模块: `path`为模块路径(不用为斜杠转义, 不用加双引号), `mod_name`为你为该模块的命名. 

```ks
// (>必须紧贴文件路径)
mod path> mod_name;
```

## 函数

比如我当前的文件夹里面有一个`module.ks`(要导入的模块)和一个`main.ks`(我要运行的ks文件)

你需要在`module.ks`中使用`mod.`语句来导出函数, 在`main.ks`使用`-.`表达式来读取模块中的函数. 

模块`module.ks`中: 

```ks
// module
const i = 90;
// 使用mod.func导出func函数
mod.func(): i;
```

主程序`main.ks`中: 

```ks
// main
// 导入module.ks文件, 
// 作为模块my_mod
mod module.ks> my_mod;
// 使用'模块名-.函数名'
// 来获取其函数
log(my_mod-.func());
```

我们运行`main.ks`, 则会得到打印的`90`. 

`Playground`中, 导入的`模块路径`和另一个模块的文件名保持一致. 

## 类型

Key语言使用`mod`关键词导出和导入类声明. 

在`module.ks`使用`mod:`语句导出类, 在`main.ks`使用`-:`表达式使用模块中的类.

类的`属性`, `实例方法`和`静态方法`都可以使用`>`前缀来代表公开性`public`. 

`module.ks`中:

```ks
// module
mod: A {
  // 使用>前缀表示
  // 模块外可直接读写此属性
  >a, 
  // 模块外可直接调用该静态方法
  >new(): A::{ a: 3 }, 
  // 模块外的该类型实例
  // 可以使用该方法
  // 得到a属性的平方
  >.test(): self.a.pow(2)
}
```

`main.ks`中: 

```ks
// main
// 导入module.ks文件, 作为模块m
mod module.ks> m;
// 使用'class A = B'创建类型的别名
// 此后A类型就是m模块的A
class A = m-:A;
// 直接构造该类型
let direct = A::{ a: 4 };
// 也可以通过静态方法构造
let new = A::new();
// 得到16 9
log(direct.test(), new.test())
```

我的`属性`, `实例方法`和`静态方法`到底需不需要公开? 哪些需要公开? 

### 公开属性

公开属性时, 模块外就可以直接读写该属性. 

当所有属性均为公开时, 模块外就能直接构造该类型的实例. 

还记得么, 使用`A::{}`的语法创建一个类的实例时, 是会自动将未填写的属性填充为`uninit`的. 

但对于别的模块导入的类型, 你必须将所有的属性填写完整才能成功构造. 

`module`中: 我们公开所有属性:

```ks
// module
mod:A {
  >a,
  >b
}
```

`main`中试着省略属性: 

```ks
// main
// 导入模块作为m
mod module.ks> m;
// 创建别名便于构造
class A = m-:A;
// 正常构造
let a = A::{a, b};
// A { a, b }
log(a);
// true
log(a is m-:A);
// 省略属性构造
// 报错: 未定义a,b属性
log(A::{});
```

也就是说只要你取消了任何一个属性的公开性, 模块外就无法直接构造该类型了. 

有趣的是, 即使你没导出这个类型, 你也可以在模块外访问这个类型实例的公开属性. 

`module.ks`:

```ks
// module
// 我们用class不用mod:
class A {
  >a
}
// 导出函数创造一个A的实例
mod.create(): A::{ a: 233 };
```

`main.ks`

```ks
// main
mod module.ks> m;
// 创建一个A实例
let a = m-.create();
// 正常打印233
log(a.a);
// 试着手动构造, 报错:
// 模块m没有A类型
m-:A::{a: 20};
```

简单来说就是, 公开属性的选择和是否导出了该类型无关, 只要你想给用户读写权就公开, 不想给就不公开. 

### 公开方法

其实和公开属性一样, 你想让模块外使用这个方法就公开, 不想就不公开. 

不过值得注意的一点是, 实例方法名和属性名完全可以同名, 因为访问属性和调用方法是两个完全没联系的行为. 

因此你可以不公开一个属性, 而是创建一个同名的方法用来让模块外读取这个值. 这样模块外就没法直接写入该属性了. 

如果你想允许用户写入该属性, 也可以创建一个叫`set_`+属性名的方法, 对传进来的参数进行判断后再写入. 

`module.ks`: 

```ks
// module
// 导出类型A
mod: A {
  // 不公开的属性a
  a: Int
  // 导出new静态方法
  // 允许模块外创建A实例
  // 别忘了结尾的逗号
  >new(): A::{ a: 3 },
  // 导出a方法用来访问a属性
  >.a(): self.a, 
  // 导出set_a用来判断并设置a属性
  >.set_a(value: Int) {
    // 我们不让a属性大于5
    if value>5 
      // 使用throw来报错
      throw "a属性不可大于5"
    // 将该值赋给a
    self.a = value;
  }
}
```

`main.ks`

```ks
// main
mod module.ks> m;
// 我们创建一个A实例
let i = m-:A::new();
// 使用`.a()`读取a属性
// 得到3
log(i.a());
// 我们将a设为1
i.set_a(1);
// a成功变为1
log(i.a());
// 报错: a属性不可大于5
i.set_a(6);
```

### 公开静态方法

公开静态方法需要你导出类, 否则无意义.

因为静态方法只能通过类名访问. 

## 结语

我对模块化的实现让模块外在使用这些类型时也能尽可能保持数据正确. 

自此, Key语言的核心概念你已经通关了! 之后将会讲一些杂项的使用. 

下一篇: <jmp to="/guide/9.async">异步操作</jmp>

## 参考

需要注意的是: 即使是同一模块, 重复导入也会重新执行模块内容, 相当于重置模块. 也就是说原则上Key语言并不会对你的模块进行缓存行为, 因此你可以安心的在自己的模块中初始化全局变量.

另外, 无论你在什么地方导入模块(不管多深的作用域), 都会共享给整个模块(也就是作用域内声明模块, 作用域外部也能使用该模块)

### 导入路径

使用`mod`导入路径时, 值得注意:

- `.ks`后缀名可以省略
- 如果发现你传入的路径是文件夹, 则会寻找该文件夹下的`/mod.ks`
- 使用相对路径时, 会以`ks`文件的位置为起点, 支持`../`作为相对路径开头
- todo: `mod+ xx> @dir`用语法添加一个搜索路径, 没标准库就先不做了
