# if

Key语言中使用`if`和`else`语句来控制流程. 语法为

```
if condition statement
else statement
```

无需在`if`后加`()`,也无需在条件后加`{}`. key在解析完条件时会直接将下一语句作为该if的语句. 

```
let max(a,b) {
  if a>b return a
  else return b
}
max(2,5) // 5
```

代码仅供演示语法, 事实上可以直接调用`a.max(b)`的内置方法来求最大值.

需要注意的是: `if`的条件后如果是单语句 且其后有`else`, `if`后的语句后是不能加`;`的, 因为`;`被解析器视为一个独立的空语句, 这也是if后不强制使用大括号的代价之一. 

如果你对此有困惑, 就无脑在条件后加个大括号包住你的语句, 这是没有副作用的. 

## 三元运算

key不使用三元运算符, 可以使用闭包(就是函数)实现类似的效果. 

```
let a = ||{
  if true: "ok"
  else: "no"
}();
```

该语句中, 原地创建了一个闭包, `||{..}`, 并在条件`true`后直接接上返回语句`: "ok"`, 提供`else`接上返回语句代表例外情况的返回值. 在运算符解析时, 不会将`:`解析为运算符. 因此你还可以:

```
if 5>3:"ok"
else "no"
```

该代码将如期运行. 


# for

## 条件循环和无条件循环

条件循环使用`for(condition)`语法, 如果你是别的语言过来的, 就把`for`代入成`while`就明白了.

```
for(condition) ..
```

无条件循环使用`for!`语法, 理解成`while (true)`的缩写

```
for! ..
```

你可以:

```
let i = 0;
for(i<20) {
  log(i);
  i += 1;
};
```

或者配合if使用:

```
let i = 0;
for! {
  if i>=20 break;
  log(i);
  i += 1;
}
```

上述代码使用了`break`语句, 代表停止循环. 你也可以用`return`语句(`: xx`)直接停止循环的同时返回函数. 

使用`continue`来跳过一次循环过程:

```
let i = 0;
for! {
  if i>=20 break;
  i += 1; // continue会阻止一次循环过程之后的代码运行
          // 不要把 +=1 写到continue之后
  if i % 2 == 0 continue;
  log(i); // 1 3 5 7 .. 19
}
```

值得注意的是, 单语句的`for`允许`let`在当前作用域上:

```
// 虽然一般没人会写成这样子, 但这也算是文档规范的一环
let i = 0;
let next() {
  i+=1;
  if i>5:false // 可别加分号
  else: true
}
for(next()) let inner = 9;

log(inner) // 9
```

在找3个参数的for? 很遗憾, 我嫌它的写法啰嗦费解就把它从Key语言切割了. 接下来登场的是更好用的迭代器:

## 迭代器

Key语言的迭代器使用`for iter ..`的语法, 也可以使用`for value: iter ..`的语法将迭代过程的值赋给过程中的`value`变量. 

现在循环20次你可以直接:

```
for 20 {
  ..
}
```

### Str

它最重要的使用场景之一是遍历字符串.

以下写法性能及其低下, 每次操作和读取都会从字符串的内存中寻找第n个字符的位置.

```
// 这是错误示范!
let s = "我是芙卡洛斯的狗"
let i = 0;
let len = s.len;
for(i<len) {
  log(s[i]);
  i+=1;
}
```

使用迭代器则能一气呵成: 

```
for char: "我是芙卡洛斯的狗" {
  log(char)
}
```

使用`lines()`方法将字符按行遍历:

```
let s = "第一行
第二行
第三行
eof";

for line: s.lines() {
  log(line + " 换行了") // 按行打印
}
```

### Int & Uint

对整数使用迭代器: 

```
for n: 10 {
  log(n) // 0,1,..,8,9
}
```

通过整数的迭代器创建数组或列表:

```
Buf::from_iter(10); // [00,01,..,08,09]
List::from_iter(10u); // [0u,1u,..8u,9u]
```

### Buf & List

```
let a = '五条悟'; // 单引号是Buf别忘了
for n: a {
  log(n) // 打印五条悟的utf8字节码
}
```

```
let a = ["哈哈","嘿嘿","呼呼"];
for s: a {
  log(s) // 哈哈, 嘿嘿, 呼呼
}
```

### Obj

直接遍历对象`Obj`得到的是键名, 你可以通过得到的键名去访问原对象的对应值. 

请注意: 哈希表是无序的, 遍历过程无法保证插入顺序或定义顺序. 

```
let o = {
  s: 9
  o: "哈哈"
};

for key: o {
  log(key, o[key])
}
```

使用`values()`和`entries()`:

```
let o = {
  s: 9
  o: "哈哈"
};

for key: o.values() {
  log(key, o[key]) // 9, 哈哈
}
for entry: o.entries() {
  log(entry) // ["s", 9], ["o", "哈哈"]
}
```

`Obj::from_list`对应的就是`List::from_iter(obj.entries())`.

### 自定义迭代器

你可以通过定义`.@next`方法为你的类型实现迭代器. 

```
let a = '{ab bc cd de}';
class A {
  i:Int,
  .@next() {
    // 如果i大于a的长度则直接返回迭代结束的标志
    if self.i>=a.len: Sym::iter_end();
    let n = a[self.i];
    self.i += 1;
    :n
  }
}

for n: A::{i:0} {
  log(n)
}
```

Key语言中有一个特别的类型: `Sym`, 代表`Symbol`, 就是标志的意思.

目前`Sym`只有一个使用方法: 作为`@next`方法的结束标志. 通过调用`Sym::iter_end()`可以得到一个迭代结束的标志.

## 另外

你需要注意, 迭代器本身不应该被存为变量, 更**不应该活的比被迭代的值久**. 以下行为会招来灾厄: 

```
let iter;
{
  let a = "启动启动启动!";
  iter = a.lines();
}
for l:iter {
  log(l);
}
```

运行没问题? 实际上只是巧合. 在`for`上一行时`a`就已经被回收了, 此时迭代的只是一片无主的内存罢了, 报不报错全看运气. 

## 结语

迭代器无疑给语法设计上提供了很大的发挥空间, 但在迭代过程中你仍然有权限修改被迭代的值, 这种行为是未定义的. 

至此, 主要的语法架构已经讲完了. 下一篇虽然也是语法介绍之一, 但恐怕实用性不算很强, 请作为番外篇进行对待. 

