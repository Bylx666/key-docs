# 集合

本篇将说明列表`List`, 数组`Buf`和哈希表`Obj`的定义和基本使用. 

## 使用列表

列表`List`是一个不定长的元素集合. 

列表使用`[]`包裹, 每个元素使用`,`分隔. 结尾可以有一个多余逗号. 列表可以存任何类型的数据. 

有趣的是, 你可以省略列表元素之间的逗号, 不过可能带来的歧义和可读性问题请自行权衡. 

```ks
let a = [5, "t", 5.5];
// [5, t, 5.5]
log(a);
```

### 空槽

不可在任何槽位留空. 

```ks
// 报错: 列表不允许空元素
[,]
```

你或许需要用`()`代替空槽. 

```ks
// 可行
debug([2, (), "ok"]);
```

### 静态方法

`类型名::函数名()`的写法被称作为`调用静态方法`. 

试试使用`静态方法`: 

```ks
// 调用List的静态方法from_iter
// 自动创建一个0-9的列表
// 打印: [0,1,..,8,9]
log(List::from_iter(10));
```

### 使用索引

列表可以使用`[i]`的写法读写一个槽位的值. `i`必须是整数`Int`或正整数`Uint`. 读取超出范围的值时会得到`uninit`, 写入超出范围的值时什么也不发生. 

```ks
let a = [2,4,6];
// 2
log(a[0]);
// 写入第二个元素
a[1] = "乐";
// [2, 乐, 6]
log(a);
```

### `len`

列表的属性`len`, 得到列表的元素数量`Uint`. 不可写入. 

```ks
let a = [2,4,6];
// 3
log(a.len);
```

### 比较

列表之间也能比较. 谁先出现更大值谁大. 

```ks
let a = [2, 4, 6];
let b = [2u, 4.0, 6];
// 数字比较不区分类型
// 因此为true
log(a==b);
```

### 解构赋值

解构时如果左侧元素数量大于右侧, 则多出的元素被赋为`uninit`. 

```ks
let [a,b,c] < [2,4];
// Int(4), uninit
debug(b, c);
```

左侧元素少于右侧则会忽略右侧多出的部分. 

细致使用请参见<jmp to="/prim/list">列表`List`</jmp>

## 使用数组

数组`Buf`代表了一片内存. 通俗来说, 就是一个数字组成的列表, 而且数字必须是`0-255`范围中的一个整数. 

数组使用单引号`''`来构造. `''`的内容会直接以`utf8`字节写入数组. 

```ks
// 单引号是数组, 不是字符串
let a = '芙卡';
// Buf[E8,8A,99,E5,8D,A1];
log(a);
```

### 内嵌字节

数组中使用`{}`来包裹一段**十六进制**值. `{}`中可以使用`/`实现单行注释. 

```ks
// 在其中嵌入'b'的字节
let a = 'a{62}c';
// Buf[61,62,63]
log(a);
```

大括号中的换行和空格会被忽略, 因此你可以用`{}`来换个行. 

```ks
// 用空大括号换行
// 否则会解析为换行符
let a = 'a{
}b{
  / 单行注释
  63 64
}';
// Buf[61,62,63,64]
log(a);
```

### 索引

数组可以通过整数索引读写内容, 并且只能写入整数. 

数组值读取时均为`Uint`类型. 超出范围的读取得到`uninit`, 超出范围的写入则不做任何事情. 

```ks
let a = '046';
// 将a的第一个元素设为
// '2'数组的第一个值
a[0] = '2'[0];
// 将数组作为utf8转换为字符串
// 打印: 246
log(a.as_utf8());
```

像列表一样, 你也可以读取`len`属性获得数组长度. 

### 比较

数组之间可以直接进行比较. 谁先出现更大的值谁大. 

```ks
let a = '{22 44 66}';
let b = '{22 45 00}';
// 第二个字节0x45 > 0x44
// 因此为true
log(a<b);
```

### 解构赋值

解构时如果左侧元素数量大于右侧, 则多出的元素被赋为`uninit`. 

```ks
let [a,b] < '{23}';
// Uint(35), uninit
debug(a, b);
```

左侧元素少于右侧则会忽略右侧多出的部分. 

细致使用请参见<jmp to="/prim/buf">数组`Buf`</jmp>

## 哈希表

哈希表`Obj`是一种使用键对储存数据的类型. 它的键为字符串`Str`, 可以有任意类型的值. 

哈希表使用`{}`字面量来创建. 哈希表的元素要写成`name: value`的格式, 每个元素间用`,`隔开. 

哈希表的元素如果只有`name`没有`: value`, 则会将其值作为`uninit`. 

哈希表元素之间的`,`可省略, 但可能会引起语法歧义, 请自行斟酌. 

```ks
let o = {
  a: 20,
  b, // 可省略':'的值
  c
};
// { a: 20, b, c }
log(o);
```

### 读写

哈希表可以使用字符串`Str`索引和属性名读写内容. 

读取不存在的值时会得到`uninit`. 写入不存在的属性时会自动插入. 

由于`uninit`的任何属性都是`uninit`, 所以可以放心读取哈希表, 不用担心读空值出错了. 

```ks
let o = {};
// 使用属性插入值
o.a = 20;
// 使用属性读取值:20
log(o.a);
// 使用字符串索引写入值
o["b"] = 40;
// {a:20,b:40}
log(o);
```

### 查删

使用`has`方法判断哈希表是否有一个元素:

```ks
// 一个只有a属性的哈希表
let o = {a};
// true false
log(o.has("a"), o.has("b"));
```

使用`remove`删除一个元素(会返回被删除的元素): 

```ks
let o = {a:"ok"};
// 删除a属性, 并把其值给rem
let rem = o.remove("a");
// 打印: ok
log(rem);
// a属性被删除, 因此是false
log(o.has("a"));
```

### 比较

哈希表是无序表, 因此只允许`==`和`!=`. 使用`>`,`<`,`>=`,`<=`均立刻返回`false`. 

```ks
// 定义俩属性
let a = {a:0.2,b:2.0};
// 把b的b属性替换成正整数
let b = {a:0.2,b:2u};
// 数字比较不区分数字类型
// 因此打印: true
log(a==b);
``` 

### 解构赋值

哈希表使用`{}`来解构. 使用不存在的属性名时会直接报错. 

`{}`中的元素间的`,`可省略. 

```ks
let {a, b} < {a:3, b:6};
// 3 6
log(a, b);
```

但也可以只解构一部分元素, 剩下元素的可以省略. 

```ks
// 只解构一部分元素也可以
let {a} < {a:3, b:6};
// 3
log(a);
// 报错: 哈希表中没有c属性
let {c} < {a:3};
```

细致使用请参见<jmp to="/prim/obj">哈希表`Obj`</jmp>

## 结语

以上就是基本集合类型. 

虽然只说明了基本定义和声明方式, 但如果你学过任何一门完整的编程语言, 你可能会在`细致使用`中发现很多熟悉的身影, 希望你能多些耐心发现自己需要的那一份, 或者大胆指出Key语言亟需改进的地方. 

我们只讲了这些集合的定义方法, 如何去遍历它们呢? 

下一篇: <jmp to="/guide/5.for">`if`和`for`</jmp>

## 参考

下述为底层比较算法. 

### 数据列表比较算法

`Buf`, `Str`, 本地类实例, `List`都属于有序数据列表. 比较是否相同的算法如下

```rust
fn compare(left: &[u8], right: &[u8]) -> bool {
  // 首先将短的buf长度作为比较对象
  let l = min(left.len(), right.len());
  // 逐位比较，只要出现一位比较另一位为false就代表比较失败返回false
  for i in 0..l {
    if !(left[i] == right[i]) {
      return false
    }
  }
  // 若每一位都相同就比较长度，长度也相同则代表两个buf全等
  return left.len() == right.len();
}
```

将`u8`替换为Key语言中的值, 结论是一样的.

比较大小算法:设两量为`a`和`b`

1. 比较长度, 长者大, 否则下一步(此时`a`,`b`应长度相同)
2. 逐位判断: 如果无法比较直接返回`false`(`!=`返回`true`), 只要出现大于直接返回大于,出现小于直接返回小于
3. 全部元素判断完成, 仍未出现大于小于或无法比较, 则是相等
